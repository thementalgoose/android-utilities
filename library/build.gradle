import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'
apply plugin: "de.mannodermaus.android-junit5"

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

group = 'com.github.thementalgoose'

android {

    namespace "tmg.utilities"

    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.resources.srcDirs += 'src/androidTest/res'
        test.java.srcDirs += 'src/test/java'
    }

    viewBinding {
        enabled true
    }

    testOptions {
        unitTests {
            includeAndroidResources  = true
            all {
                afterSuite { desc, result ->
                    if (!desc.parent) {
                        println "\n======================================================================="
                        println "${desc.displayName}"
                        println "Test result: ${result.resultType}"
                        println "Test summary: ${result.testCount} tests, " +
                                "${result.successfulTestCount} succeeded, " +
                                "${result.failedTestCount} failed, " +
                                "${result.skippedTestCount} skipped"
                        println "======================================================================="
                    }
                }
                useJUnitPlatform()
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                }
            }
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.kotlin.stdlib
    implementation libs.androidx.lifecycle.extensions
    testImplementation libs.junit.jupiter.core
    testImplementation libs.junit.jupiter.engine
    testImplementation libs.junit.vintage

    // AndroidX
    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.ui
    implementation libs.androidx.preferences
    implementation libs.material
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.recyclerview
    implementation libs.androidx.annotation
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.viewpager2

    // ViewModel
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.livedata

    // ThreeTen
    implementation libs.threeten.abp
    testImplementation (libs.threeten.core) {
        exclude group:'com.jakewharton.threetenabp', module:'threetenabp'
    }

    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.test
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}
